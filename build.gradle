plugins {
  id 'idea'
  id 'java-library'
  id 'maven'
}

defaultTasks 'check', 'test', 'install'

group = 'com.codeborne'
description = 'Selenide adaptor for Appium framework'
version = '1.6.1'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
  encoding = 'UTF-8'
  log4jVersion = '2.13.0'
}

compileJava {
  options.incremental = true
  options.encoding = encoding
}

compileTestJava {
  options.encoding = encoding
}

javadoc {
  options.encoding = encoding
  failOnError = false
}

repositories {
  jcenter()
  mavenCentral()
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots"
    mavenContent {
      snapshotsOnly()
    }
  }
}

dependencies {
  api('com.codeborne:selenide:5.17.4')
  api('io.appium:java-client:7.4.1')
  implementation('net.bytebuddy:byte-buddy:1.10.19')
  testImplementation('org.junit.jupiter:junit-jupiter:5.7.0')
  testImplementation("org.assertj:assertj-core:3.18.1")
  testImplementation('org.mockito:mockito-core:3.7.0')
  testRuntimeOnly('org.slf4j:slf4j-simple:1.7.30')
}

task libs(type: Sync) {
  from configurations.compileClasspath
  from configurations.testCompile
  from configurations.testRuntimeClasspath
  into "$buildDir/lib"
}

tasks.withType(Test) {
  useJUnitPlatform()
}

compileJava.dependsOn libs

jar {
  manifest {
    attributes(
      "Implementation-Title": project.group + '.' + project.name,
      "Implementation-Version": version,
      "Implementation-Vendor": "Codeborne")
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

apply from: file('gradle/deploy.gradle')
